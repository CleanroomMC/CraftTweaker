buildscript {
    repositories {
        mavenCentral()
    }
}

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group = "com.blamejared.crafttweaker"
version = "1.0.0"
archivesBaseName = 'Crafttweaker_Annotation_Processors-1.16.5'
if (System.getenv('BUILD_NUMBER') != null) {
    version += "." + System.getenv('BUILD_NUMBER')
}

subprojects {
    apply from: rootProject.file('common.gradle')
}

def projectDeps = [":Crafttweaker_Annotations", ":JavaAnnotations"]

dependencies {
    projectDeps.collect { implementation project(it) }

    implementation 'org.jetbrains:annotations:22.0.0'
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.10'
    implementation files('libs/tools.jar')
    implementation 'com.google.code.gson:gson:2.8.9'
}

jar {
    dependsOn.addAll(projectDeps.collect { "$it:compileJava" })
    from files(projectDeps.collect { project(it).sourceSets.main.output })
}

publishing {

    publications {

        mavenJava(MavenPublication) {

            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java

            // Allows the maven pom file to be modified.
            pom.withXml {

                // Go through all the dependencies.
                asNode().dependencies.dependency.each { dep ->

                    println 'Surpressing artifact ' + dep.artifactId.last().value().last() + ' from maven dependencies.'
                    assert dep.parent().remove(dep)
                }
            }

        }
    }

    repositories {

        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}