pluginManagement {
    repositories {
        gradlePluginPortal()
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'Sponge Snapshots'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }
}

rootProject.name = 'CraftTweaker'
include("Common", "Fabric", "Forge")

include("ZenCode")
include("Crafttweaker_Annotations")
include("Crafttweaker_Annotation_Processors")

def excludedProjects = [
        "ModuleDeserializer",
        "ModuleSerializationShared",
        "ModuleSerializer",
        "JavaSource",
        "JavaSourceCompiler",
        "IDE",
        "Constructor",
        "DrawableGui",
        "DrawableGuiIconConverter",
        "CompilerShared"
]

def fileFilter = new FileFilter() {
    boolean accept(File file) {
        if (!file.isDirectory()) {
            return false
        }
        if (excludedProjects.contains(file.name)) {
            return false
        }
        return new File(file, 'build.gradle').isFile()
    }
};

new File(rootDir, "Annotations").listFiles(fileFilter).each { File dir ->
    include(":" + dir.name)
    project(":" + dir.name).projectDir = new File("./Annotations/" + dir.name)
}

new File(rootDir, "ZenCode").listFiles(fileFilter).each { File dir ->
    include(":" + dir.name)
    project(":" + dir.name).projectDir = new File("./ZenCode/" + dir.name)
}

